/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2023 The RIOT authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.ajoberstar:gradle-git-publish:$gitPluginVersion"
        classpath "org.jreleaser:jdks-gradle-plugin:$jdksPluginVersion"
        classpath "org.kordamp.gradle:guide-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:java-project-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:jacoco-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:kordamp-parentbuild:$kordampBuildVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$bootPluginVersion"
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyPluginVersion"
    }
}

apply plugin: 'org.kordamp.gradle.kordamp-parentbuild'

rootProject.name = 'riot'

projects {
    directories = ['core', 'connectors', 'plugins', 'docs']

    plugins {
        all {
            id 'eclipse'
        }
        path(':') {
            id 'org.kordamp.gradle.java-project'
        }
        dirs(['core', 'connectors', 'plugins']) {
            id 'java-library'
        }
        dirs(['core', 'connectors']) {
            id 'org.springframework.boot'
            id 'io.spring.dependency-management'
        }
    }
}

enforce {
    rule(enforcer.rules.RequireJavaVersion) { r ->
        r.version.set('17')
    }
    rule(enforcer.rules.EnforceBytecodeVersion) { r ->
        // search only on compile and runtime classpaths
        r.configurations.addAll(['compileClasspath', 'runtimeClasspath'])
        r.maxJdkVersion = '17'
    }
    rule(enforcer.rules.BanDuplicateClasses) { r ->
        r.enabled = false
    }
    rule(enforcer.rules.DependencyConvergence) { r ->
        r.enabled = false
    }
}