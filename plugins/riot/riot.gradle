/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2023 The RIOT authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'application'
    id 'org.jreleaser.jdks'
}

application {
    applicationName = 'riot'
    mainClass       = 'com.redis.riot.Main'
}

jar {
    manifest {
        attributes([
            'Main-Class': 'com.redis.riot.Main',
            'Automatic-Module-Name': project.findProperty('automatic.module.name')
        ])
    }
}

startScripts {
    classpath = files('$APP_HOME/lib/*')
}

dependencies {
    api(project(':riot-core')) {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    api project(':riot-file')
    api project(':riot-faker')
    api group: 'com.redis', name: 'spring-batch-redis-core', version: springBatchRedisVersion
    api group: 'com.redis', name: 'spring-batch-resource', version: springBatchRedisVersion
    implementation group: 'com.redis', name: 'lettucemod', version: lettucemodVersion
    implementation group: 'org.latencyutils', name: 'LatencyUtils', version: latencyUtilsVersion
    implementation 'org.springframework.batch:spring-batch-infrastructure'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.springframework:spring-jdbc'
    implementation 'com.zaxxer:HikariCP'
    implementation 'com.microsoft.sqlserver:mssql-jdbc'
    implementation 'com.mysql:mysql-connector-j'
    implementation 'com.oracle.database.jdbc:ojdbc11'
    implementation 'org.postgresql:postgresql'
    testImplementation group: 'com.redis', name: 'spring-batch-redis-test', version: springBatchRedisVersion
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc'
    testImplementation 'org.awaitility:awaitility'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:oracle-xe'
    testImplementation 'org.hsqldb:hsqldb'
}

distributions {
    main {
        contents {
            from('../..') {
                include 'LICENSE'
                include 'VERSION'
            }
        }
    }
}

jdks {
    zulu17Linux {
        platform = 'linux-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu17.50.19-ca-jdk17.0.11-linux_x64.zip'
        checksum = 'c89977771091eb7d53e0f244a722076e88d968966b75aba2a65a1b31105d68bb'
    }
    zulu17LinuxArm {
        platform = 'linux-aarch64'
        url = 'https://cdn.azul.com/zulu/bin/zulu17.50.19-ca-jdk17.0.11-linux_aarch64.tar.gz'
        checksum = '518cc455c0c7b49c0ae7d809c0bb87ab371bb850d46abb8efad5010c6a06faec'
    }
    zulu17LinuxMusl {
        platform = 'linux_musl-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu17.50.19-ca-jdk17.0.11-linux_musl_x64.tar.gz'
        checksum = '22bbde6e05ced1238b302e2b44d3f9785c23e4115618eea83786b6b4a1828f9f'
    }
    zulu17LinuxMuslArm {
        platform = 'linux_musl-aarch64'
        url = 'https://cdn.azul.com/zulu/bin/zulu17.50.19-ca-jdk17.0.11-linux_musl_aarch64.tar.gz'
        checksum = 'fc6645aa46736e6100302c109127ea31a1d1192599b7140dd94f81eaeebfe293'
    }
    zulu17Windows {
        platform = 'windows-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu17.50.19-ca-jdk17.0.11-win_x64.zip'
        checksum = '43f0f1bdecf48ba9763d46ee7784554c95b442ffdd39ebd62dc8b297cc82e116'
    }
    zulu17WindowsArm {
        platform = 'windows-aarch64'
        url = 'https://cdn.azul.com/zulu/bin/zulu17.50.19-ca-jdk17.0.11-win_aarch64.zip'
        checksum = 'b8833d272eb31f54f8c881139807a28a74de9deae07d2cc37688ff72043e32c9'
    }
    zulu17Osx {
        platform = 'osx-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu17.50.19-ca-jdk17.0.11-macosx_x64.zip'
        checksum = '3f653ad00a29b3ae2abebcfc598225f674d3289e52497b310441f3fb5d785f8a'
    }
    zulu17OsxArm {
        platform = 'osx-aarch64'
        url = 'https://cdn.azul.com/zulu/bin/zulu17.50.19-ca-jdk17.0.11-macosx_aarch64.zip'
        checksum = '5aec103fc90131dc34cb1be6290dd61c426a74f1bf7e8b4895049cd27fee70a8'
    }
}

bootStartScripts.dependsOn jar

afterEvaluate {
    def copyJdksToCache = project.tasks.findByName('copyJdksToCache')
    ['zulu17Linux', 'zulu17LinuxArm', 'zulu17LinuxMusl', 'zulu17LinuxMuslArm',
     'zulu17Windows', 'zulu17WindowsArm', 'zulu17Osx', 'zulu17OsxArm'].each { jdk ->
        def copyTask = project.tasks.findByName('copyJdkFromCache' + jdk.capitalize())
        if (copyJdksToCache && copyTask) copyTask.dependsOn(copyJdksToCache)
    }
}

mainClassName = "com.redis.riot.Main"

task copyRuntimeDependencies(type: Copy) {
    into "$buildDir/dependencies/flat"
    from configurations.runtimeClasspath
}

assemble.dependsOn(copyRuntimeDependencies)